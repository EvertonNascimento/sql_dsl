grammar org.lunifera.metamodel.dsl.SqlDSL with org.eclipse.xtext.common.Terminals

generate sqlDSL "http://www.lunifera.org/metamodel/dsl/SqlDSL"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

SModel:
	(settings=SSettings)?
	artifact+=SArtifact*;

SSettings:
	{SSettings}
	'settings {'
	(('schema=' schema=ID)? &
	('engine=' engine=SDBEngine)?)
	'}';

enum SDBEngine:
	INNODB="InnoDB" | MYISAM="MyISAM";

SArtifact:
	STable | SEnum;

STable:
	'table' name=ID '{'
	('prefix' prefix=ID ';')?
	entityMembers+=SEntityMember*
	'}';

SEntityMember:
	SProperty | SJoinProperty;

SProperty:
	'column' name=ID (extType=[SExtDeclaredSQLType] | inlinedType=SInlinedSQLType | simpleType=SSimpleTypes)
	(props=SColumnProps)? ';';

SJoinProperty:
	'joincolumn' name=ID type=[STable] (props=SColumnProps)? ';';

SColumnProps:
	{SColumnProps}
	'with'
	((nullable?='nullable')? &
	(aes?='AES')? &
	('index=' index=SIndex)?);

SExtDeclaredSQLType:
	SEnum;

SInlinedSQLType:
	SString | SDecimal;

enum SSimpleTypes:
	INT='Int' |
	TINY_INT='Tinyint' |
	SMALL_INT='Smallint' |
	MEDIUM_INT='Mediumint' |
	BOOLEAN='Boolean' |
	BLOB='Blob' |
	DATETIME='Datetime' |
	DATE='Date' |
	TIME='Time' |
	POLYGON='Polygon' |
	POINT='Point' |
	FOTO='Foto' |
	Currency='Currency' |
	Coordinate='Coordinate';

enum SIndex:
	NO="No" | YES="Yes" | UNIQUE="Unique" | SPATIAL="Spatial";

SEnum:
	'enum' name=ID
	'{'
	(literals+=SEnumLiteral)*
	'}';

SEnumLiteral:
	name=ID '=' value=INT;

SString:
	'String(' value=INT ')';

SDecimal:
	'Decimal(' value=INT ')';
