grammar org.lunifera.metamodel.dsl.SqlDSL with org.eclipse.xtext.common.Terminals

generate sqlDSL "http://www.lunifera.org/metamodel/dsl/SqlDSL"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

SModel:
	('generatedFile' generatedFile=ID)
	(settings=SSettings)?
	artifact+=SArtifact*;

SSettings:
	{SSettings}
	'settings {'
	(('schema=' schema=ID)? &
	('javapackage=' javapackage=LFQN)? &
	('engine=' engine=SDBEngine)?)
	'}';

enum SDBEngine:
	INNODB="InnoDB" | MYISAM="MyISAM";

SArtifact:
	STable | SEnum;

STable:
	'table' name=ID '{'
	((settings=SSettings)? &
	('entityname=' entityname=ID)? &
	(cached?='cached')? &
	('prefix' prefix=ID ';')?)
	columns+=STableMember*
	'}';

STableMember:
	SColumn | SJoinColumn;

SColumn:
	'column' name=ID (extType=[SExtDeclaredSQLType] | inlinedType=SInlinedSQLType | simpleType=SSimpleTypes)
	(props=SColumnProps)? ';';

SJoinColumn:
	'joincolumn' name=ID referencedType=[STable] (props=SColumnProps)? ';';

SColumnProps:
	{SColumnProps}
	'with'
	((nullable?='nullable')? &
	(aes?='AES')? &
	('index=' index=SIndex)? &
	('javacolumn=' javacolumn=ID)?);

SExtDeclaredSQLType:
	SEnum;

SInlinedSQLType:
	SString | SDecimal;

enum SSimpleTypes:
	INT='Int' |
	TINY_INT='Tinyint' |
	SMALL_INT='Smallint' |
	MEDIUM_INT='Mediumint' |
	BOOLEAN='Boolean' |
	BLOB='Blob' |
	DATETIME='Datetime' |
	DATE='Date' |
	TIME='Time' |
	POLYGON='Polygon' |
	POINT='Point' |
	FOTO='Foto' |
	Currency='Currency' |
	Coordinate='Coordinate';

enum SIndex:
	NO="No" | YES="Yes" | UNIQUE="Unique" | SPATIAL="Spatial";

SEnum:
	'enum' name=ID
	'{'
	(literals+=SEnumLiteral)*
	'}';

SEnumLiteral:
	name=ID '=' value=INT;

SString:
	'String(' value=INT ')';

SDecimal:
	'Decimal(' value=INT ')';

LFQN:
	ID ('.' ID)*;